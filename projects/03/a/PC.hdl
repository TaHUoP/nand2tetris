// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
        Or(a=inc, b=load, out=incOrLoad);
        Or(a=incOrLoad, b=reset, out=shouldChangeVal);
        Mux16(a=currentVal, b=incVal, sel=inc, out=maybeIncVal);
        Mux16(a=maybeIncVal, b=in, sel=load, out=maybeIncOrLoad);
        Mux16(a=maybeIncOrLoad, b=false, sel=reset, out=maybeResetOrIncIn);

        Inc16(in=currentVal, out=incVal);
        Register(in=maybeResetOrIncIn, load=shouldChangeVal, out=currentVal, out=out);
}
